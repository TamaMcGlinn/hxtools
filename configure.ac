
AC_INIT([hxtools], [20111204])
AC_CONFIG_AUX_DIR([build-aux])
AC_CONFIG_HEADERS([config.h])
AC_PROG_INSTALL
AM_INIT_AUTOMAKE([-Wall foreign subdir-objects tar-pax no-dist-gzip dist-xz])
AC_PROG_CC
AM_PROG_CC_C_O

AC_CHECK_HEADERS([lastlog.h paths.h])
PKG_CHECK_MODULES([libHX], [libHX >= 3.12.1])
AC_SEARCH_LIBS([cap_get_flag], [cap], [libcap_LIBS="$LIBS"; LIBS=""])
AC_SUBST([libcap_LIBS])
AM_CONDITIONAL([HAVE_LIBCAP], [test -n "$libcap_LIBS"])
AC_SEARCH_LIBS([dlopen], [dl], [libdl_LIBS="$LIBS"; LIBS=""])
AC_SUBST([libdl_LIBS])

kbddir='${pkgdatadir}/kbd';
keymapdir='${kbddir}';
vgafontdir='${kbddir}';
x11fontdir='${kbddir}';
AC_ARG_WITH([slibdir], AS_HELP_STRING([--with-slibdir=PATH],
	[Path to the super lib directory [[/lib]]]),
	[slibdir="$withval"], [slibdir="/lib"])
AC_SUBST([slibdir])
AC_ARG_WITH([keymapdir], AS_HELP_STRING([--with-keymapdir=PATH],
	[Path to the keymap directory.
	Most likely /usr/share/kbd/keymaps.]),
	[keymapdir="$withval"])
AC_ARG_WITH([vgafontdir], AS_HELP_STRING([--with-vgafontdir=PATH],
	[Path to the VGA font directory.
	Most likely /usr/share/kbd/consolefonts.]),
	[vgafontdir="$withval"])
AC_ARG_WITH([x11fontdir], AS_HELP_STRING([--with-x11fontdir=PATH],
	[Path to the X11 font directory.
	Most likely /usr/share/fonts.]),
	[x11fontdir="$withval"])
AC_SUBST([kbddir])
AC_SUBST([keymapdir])
AC_SUBST([vgafontdir])
AC_SUBST([x11fontdir])

regular_CPPFLAGS="-D_FILE_OFFSET_BITS=64 -D_REENTRANT"
regular_CFLAGS="-Wall -Waggregate-return -Wmissing-declarations \
	-Wmissing-prototypes -Wredundant-decls -Wshadow -Wstrict-prototypes \
	-Winline -pipe"
AC_SUBST([regular_CPPFLAGS])
AC_SUBST([regular_CFLAGS])
AC_CONFIG_FILES([Makefile data/Makefile doc/Makefile
	kbd/Makefile sadmin/Makefile sdevel/Makefile smath/Makefile
	smm/Makefile suser/Makefile])
AC_OUTPUT
